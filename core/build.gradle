def packageName = "meteor-core"
def packageDescription = "Real-time message streaming over Hazelcast IMDG"

dependencies {
  api project(":meteor-elect")
  api "com.obsidiandynamics.fulcrum:fulcrum-nodequeue:${fulcrumVersion}"
  
  testImplementation project(":")
  testImplementation project(":meteor-assurance")
  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-testmark:${fulcrumVersion}"
  testImplementation "org.hdrhistogram:HdrHistogram:${hdrhistogramVersion}"

  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-launcher:${fulcrumVersion}"
}

task launch(dependsOn: testClasses) {
  doLast {
    def launcherClasses = new ArrayList()
    launcherClasses += "com.obsidiandynamics.meteor.PubSubOneWayTest"
    launcherClasses += "com.obsidiandynamics.meteor.PubSubRoundTripTest"
    
    def perfJvmArgs = "-XX:-MaxFDLimit -XX:+TieredCompilation -XX:+UseNUMA -XX:+UseCondCardMark " + 
                      "-XX:+UseBiasedLocking -Xms2G -Xmx2G -Xss1M -XX:+UseG1GC -XX:MaxGCPauseMillis=200 " + 
                      "-XX:InitiatingHeapOccupancyPercent=0 -Djava.net.preferIPv4Stack=true " + 
                      "-Dlauncher.package.compress.level=3"
    javaexec {
      systemProperties = System.properties
      classpath = sourceSets.test.runtimeClasspath
      main = "com.obsidiandynamics.launcher.Launcher"
      args = launcherClasses
      jvmArgs = Arrays.asList(perfJvmArgs.split(" ")) + baseJvmArgs
      standardInput = System.in
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = packageName
      from components.java
      pom {
        name = packageName
        description = packageDescription
        url = 'https://github.com/obsidiandynamics/meteor'
        licenses {
          license {
            name = 'BSD 3-Clause License'
            url = 'https://opensource.org/licenses/BSD-3-Clause'
          }
        }
        developers {
          developer {
            id = 'ekoutanov'
            name = 'Emil Koutanov'
            email = 'ekoutanov@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/obsidiandynamics/meteor.git'
          developerConnection = 'scm:git:ssh://github.com/obsidiandynamics/meteor.git'
          url = 'https://github.com/obsidiandynamics/meteor'
        }
      }
    }
  }
}