plugins {
  id "com.github.ben-manes.versions" version "0.22.0"
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
      snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
      username = findProperty('sonatype.username')
      password = findProperty('sonatype.password')
    }
  }
}
  
def baseJvmArgs = [
  "--add-opens=java.base/java.io=ALL-UNNAMED",
  "--add-opens=java.base/java.lang=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.invoke=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
  "--add-opens=java.base/java.nio=ALL-UNNAMED",
  "--add-opens=java.base/java.util=ALL-UNNAMED",
  "--add-opens=java.base/java.util.concurrent.atomic=ALL-UNNAMED",
  "--add-opens=java.base/sun.nio.ch=ALL-UNNAMED",
  "-XX:-MaxFDLimit"
]
project.ext.set("baseJvmArgs", baseJvmArgs)

allprojects {
  apply plugin: "java-library"
  apply plugin: "jacoco"
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  group = "com.obsidiandynamics.meteor"
  version = "0.25.0-SNAPSHOT"
  
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  //TODO remove when Javadoc errors have been resolved
  javadoc.options.addStringOption('Xdoclint:none', '-quiet')

  repositories {
    mavenCentral()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots"
    }
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  signing {
    sign publishing.publications
  }
  
  ext {
    commonsLangVersion = "3.9"
    equalsverifierVersion = "3.10"
    fulcrumVersion = "0.37.0"
    hdrhistogramVersion = "2.1.11"
    junitVersion = "4.12"
    hazelcastVersion = "3.12.2"
    log4jVersion = "1.2.17"
    mockitoVersion = "3.0.0"
    slf4jVersion = "1.7.25"
    yconfVersion = "0.21.0"
    zerologVersion = "0.31.0"
  }
  
  dependencies {
    compileOnly "com.hazelcast:hazelcast:${hazelcastVersion}"
    
    testImplementation "com.hazelcast:hazelcast:${hazelcastVersion}"
    testImplementation "com.hazelcast:hazelcast:${hazelcastVersion}:tests"
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-assert:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-await:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-junit:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.yconf:yconf-snakeyaml:${yconfVersion}"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "nl.jqno.equalsverifier:equalsverifier:${equalsverifierVersion}"

    testImplementation "com.obsidiandynamics.zerolog:zerolog-slf4j17:${zerologVersion}"
    testImplementation "org.slf4j:slf4j-api:${slf4jVersion}"
    testImplementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    testImplementation "log4j:log4j:${log4jVersion}"
  }
    
  test {
    exclude "**/*IT.class"
    jvmArgs += baseJvmArgs
  }
  
  jacoco {
    toolVersion = "0.8.8"
  }
  
  jacocoTestReport {
    additionalSourceDirs.from = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(sourceSets.main.output)
    reports {
      html.enabled true
      xml.enabled true
      csv.enabled false
    }
  }
}

subprojects {
  dependencies {
    testImplementation project(":").sourceSets.test.output
  }
}

task jacocoMerge(type: JacocoMerge) {
  mustRunAfter = allprojects.test
  executionData = files(allprojects.jacocoTestReport.executionData)
  doFirst {
    executionData = files(files(allprojects.jacocoTestReport.executionData).findAll { it.exists() })
  }
}

task jacocoRootReport(type: JacocoReport) {
  dependsOn jacocoMerge
  mustRunAfter = allprojects.test
  additionalSourceDirs.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(allprojects.sourceSets.main.output)
  executionData jacocoMerge.destinationFile
  reports {
    html.enabled true
    xml.enabled true
    csv.enabled false
  }
  onlyIf = {
    true
  }
  afterEvaluate {
    classDirectories.from = files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ["sample/**", "**/*Uncovered*"])
    })
  }
}

def packageName = "meteor-common"
def packageDescription = "Common classes and utilities"

dependencies {
  api "com.obsidiandynamics.fulcrum:fulcrum-retry:${fulcrumVersion}"
  api "com.obsidiandynamics.fulcrum:fulcrum-worker:${fulcrumVersion}"
  api "com.obsidiandynamics.yconf:yconf-core:${yconfVersion}"
  api "com.obsidiandynamics.zerolog:zerolog-core:${zerologVersion}"
  api "com.obsidiandynamics.zerolog:zerolog-bridge-hazelcast:${zerologVersion}"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = packageName
      from components.java
      pom {
        name = packageName
        description = packageDescription
        url = 'https://github.com/obsidiandynamics/meteor'
        licenses {
          license {
            name = 'BSD 3-Clause License'
            url = 'https://opensource.org/licenses/BSD-3-Clause'
          }
        }
        developers {
          developer {
            id = 'ekoutanov'
            name = 'Emil Koutanov'
            email = 'ekoutanov@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/obsidiandynamics/meteor.git'
          developerConnection = 'scm:git:ssh://github.com/obsidiandynamics/meteor.git'
          url = 'https://github.com/obsidiandynamics/meteor'
        }
      }
    }
  }
}